#INCLUDE "hostio.inc"
#USE "hostio.lib"
#USE "string.lib"

#INCLUDE "la.inc"

PROC open.float.data (CHAN OF SP fs, ts, VAL INT n, INT32 sid, BYTE res)
  VAL file.prefix IS "e:\data\f_" :
  IF
    (n < 1) OR (n > no.sample)
      SEQ
	sid := -1 (INT32)
	res := spr.operation.failed
    TRUE
      [40]BYTE fn :
      INT len :
      SEQ
	len := 0
	append.text (len, fn, file.prefix)
	append.int (len, fn, n / 10, 1)
	append.int (len, fn, n REM 10, 1)
	append.text (len, fn, ".bin")
	append.char (len, fn, 0 (BYTE))
	so.open (fs, ts, fn, spt.binary, spm.input, sid, res)
	IF
	  res <> spr.ok
	    sid := -1 (INT32)
	  TRUE
	    SKIP
:

PROC read.float.data (CHAN OF SP fs, ts, VAL INT32 sid,
		      [no.input]REAL32 fdat, BYTE res)
  INT len :
  VAL SZ.REAL32 IS 4 :
  [132]REAL32 tf :
  SEQ
    len := 132 * SZ.REAL32
    [132 * SZ.REAL32]BYTE b RETYPES tf :
    so.read (fs, ts, sid, len, b)
    IF
      len = (132 * SZ.REAL32)
	SEQ
	  SEQ i = 0 FOR 12
	    [fdat FROM i*10 FOR 10] := [tf FROM (i*11)+1 FOR 10]
	  res := spr.ok
      TRUE
	res := spr.operation.failed
:

PROC close.float.data (CHAN OF SP fs, ts, VAL INT32 sid, BYTE res)
  SEQ
    so.close (fs, ts, sid, res)
:

PROC open.all.float.data (CHAN OF SP fs, ts,
			  [no.sample]INT32 sid, [no.sample]BYTE res,
			  BOOL success)
  SEQ
    success := TRUE
    SEQ i = 0 FOR no.sample
      SEQ
	open.float.data (fs, ts, i+1, sid[i], res[i])
	success := success AND (res[i] = spr.ok)
:

PROC close.all.float.data (CHAN OF SP fs, ts,
			   VAL [no.sample]INT32 sid, [no.sample]BYTE res,
			   BOOL success)
  SEQ
    success := TRUE
    SEQ i = 0 FOR no.sample
      IF
	res[i] = spr.ok
	  SEQ
	    close.float.data (fs, ts, sid[i], res[i])
	    success := success AND (res[i] = spr.ok)
	TRUE
	  SKIP
:
